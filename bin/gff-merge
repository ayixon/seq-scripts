#!/usr/bin/env perl
# Created: 17 Oct 2019
# Author: Thomas Hackl, thackl@lim4.de
use warnings;
use strict;
use Getopt::Long  qw(:config no_ignore_case bundling);
use Data::Dumper;
use File::Temp qw(tempfile);

GetOptions (
    "out|o=s" => sub { '-' ne $_[1] and open(STDOUT, '>', $_[1]) || die $! },
    "keep-fasta|k" => \(my $keep_fasta),
    "help|h!" => \(my $help),
    "debug|D!" => \(my $debug),
) or die("Error in command line arguments\n");

if ($help || @ARGV < 2){
    print "Usage: gff-merge a.gff b.gff [c.gff ...] > abc.gff\n";
    printf " %-19s  %s\n", "-o/--out", "write to this file [STDOUT]";
    printf " %-19s  %s\n", "-k/--keep-fasta", "keep FASTA sections of each gff and append at the end [OFF]";
    printf " %-19s  %s\n", "-h/--help", "show this help";
    printf " %-19s  %s\n", "-D/--debug", "show debug messages";
    print "\n";
    print "Note, all directives (^##) will appear at the start, all sequences (^##FASTA)\n";
    print "at the end of the new file, and all simple comments (^#) in place.\n";
    exit 0;
}

# head, body, fasta
my $gff_version;
my ($hh, $hf) = tempfile();
my ($bh, $bf) = tempfile();
my ($fh, $ff) = tempfile() if $keep_fasta;

# '##gff-version 3'
while (<>) {
    if (/^##FASTA/){
        if (!$keep_fasta){
            close ARGV
        }else {
            my $i=1;
            while (<>) {
                print $fh $_;
                last if eof(ARGV);
            }
        }
    }elsif(/^##gff-version/){
        chomp();
        if ($gff_version) {
            $gff_version eq $_ || die "Different gff versions detected, won't merge\n".
                "  exp: '$gff_version'\n  got: '$_' at $ARGV";
        }else {
            $gff_version = $_;
        }
    }elsif(/^##/) { # directives - collect all in head
        print $hh $_;
    }else {
        print $bh $_;
    }
}continue {
    print STDERR "processed $ARGV\n" if eof(ARGV);
}

# print head and body
print STDERR "writing directives\n";
print $gff_version, "\n";
seek $hh, 0, 0 or die "Seek $hh failed: $!\n";
print while <$hh>;
print STDERR "writing features\n";
seek $bh, 0, 0 or die "Seek $bh failed: $!\n";
print while <$bh>;

# write all fasta seqs from temp file
if ($keep_fasta && tell($fh) > 0) { # only if something is in temp file
    print STDERR "writing sequences\n";
    print "##FASTA\n";
    seek $fh, 0, 0 or die "Seek $fh failed: $!\n";
    print while <$fh>;
}
