#!/usr/bin/env perl
# Created: 17 Oct 2019
# Author: Thomas Hackl, thackl@lim4.de
use warnings;
use strict;
use Getopt::Long  qw(:config no_ignore_case bundling);
use Data::Dumper;
use File::Temp qw(tempfile);

GetOptions (
    "out|o=s" => sub { '-' ne $_[1] and open(STDOUT, '>', $_[1]) || die $! },
    "keep-fasta|k" => \(my $keep_fasta),
    "help|h!" => \(my $help),
    "debug|D!" => \(my $debug),
) or die("Error in command line arguments\n");

if ($help || @ARGV > 2){
    print "Usage: gff-merge.pl < in > out\n";
    printf " %-19s  %s\n", "-o/--out", "write to this file [STDOUT]";
    printf " %-19s  %s\n", "-k/--keep-fasta", "keep FASTA sections of each gff and append at the end [OFF]";
    printf " %-19s  %s\n", "-h/--help", "show this help";
    printf " %-19s  %s\n", "-D/--debug", "show debug messages";
    exit 0;
}

my ($fh, $fa) = tempfile() if $keep_fasta;

# '##gff-version 3'
while (<>) {
    if (/^##FASTA/){
        if (!$keep_fasta){
            close ARGV
        }else {
            my $i=1;
            while (<>) {
                print STDERR "\r fasta ".$i++;
                print $fh $_;
                last if eof(ARGV);
            }
            print STDERR "\n";
        }
        next;
    }
    print $_;
}

print STDERR "appending ##FASTA\n";
# write all fasta seqs from temp file
if ($keep_fasta && tell($fh) > 0) { # only if something is in temp file
    print "##FASTA\n";
    seek $fh, 0, 0 or die "Seek $fh failed: $!\n";
    print while <$fh>;
}
